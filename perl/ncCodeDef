#!/usr/bin/perl -w

#------------------------------------------------------------------------------
#          Harvard University Atmospheric Chemistry Modeling Group            !
#------------------------------------------------------------------------------
#BOP
#
# !MODULE: ncCodeDef
#
# !DESCRIPTION: This Perl script creates Fortran code to open a netCDF
#  file and define variables and attributes.  Calls to routines from
#  the tines from the NcdfUtilities package.  
#\\
#\\
# !USES:
#
  require 5.003;        # need this version of Perl or newer
  use strict 'refs';    # Do not allow symbolic references
  use strict 'subs';    # Treat all barewords as syntax errors 
#
# !PUBLIC DATA MEMBERS:
#
  $F_ID = "";           # netCDF file ID
#
# !CALLING SEQUENCE:
# ncCodeCreate RESOURCE-FILE-NAME
#
# !REVISION HISTORY: 
#  27 Jan 2012 - R. Yantosca - Initial version
#EOP
#------------------------------------------------------------------------------
#          Harvard University Atmospheric Chemistry Modeling Group            !
#------------------------------------------------------------------------------
#BOP
#
# !IROUTINE: trim
#
# !DESCRIPTION:  Routine trim removes leading and trailing whitespace from
#  a string (analogous to IDL's Strtrim( str, 2 ) command).
#\\
#\\
# !INTERFACE:
#
sub trim($) {
#
# !CALLING SEQUENCE:
# $string = &trim( $string );
#
# !REMARKS:
#  Found online at this URL:
#  http://www.somacon.com/p114.php
#
# !REVISION HISTORY:
#  27 Jan 2012 - R. Yantosca - Initial version
#EOP
#------------------------------------------------------------------------------
#BOC

  # Shift the @_ array 
  my $string = shift;

  # Remove leading whitespace
  $string    =~ s/^\s+//;

  # Remove trailing whitespace
  $string    =~ s/\s+$//;

  # Return
  return( $string );
}
#EOP
#------------------------------------------------------------------------------
#          Harvard University Atmospheric Chemistry Modeling Group            !
#------------------------------------------------------------------------------
#BOP
#
# !IROUTINE: splitLine
#
# !DESCRIPTION:  Routine splitLine splits a line on a given delimiter, and
#  strips white space.  Convenience wrapper for the Perl "split" function.
#\\
#\\
# !INTERFACE:
#
sub splitLine($$) {
#
# !INPUT PARAMETERS:
#
  # Line to be split, and the delimeter character
  # Don't strip the white from $value if $noSplitVal==1
  my( $line, $delim ) = @_;
#
# !CALLING SEQUENCE:
# ( $name, $value ) = &splitLine( $line );
#
# !REVISION HISTORY:
#  27 Jan 2012 - R. Yantosca - Initial version
#EOP
#------------------------------------------------------------------------------
#BOC
#
# !LOCAL VARIABLES:
#
  # Split the line
  my @subStr = split( $delim, $line );
  my $name   = &trim( $subStr[0] );
  my $value  = &trim( $subStr[1] );
 
  # Return substrings
  return( $name, $value );
}
#EOP
#------------------------------------------------------------------------------
#          Harvard University Atmospheric Chemistry Modeling Group            !
#------------------------------------------------------------------------------
#BOP
#
# !IROUTINE: readRcFile
#
# !DESCRIPTION:  Routine readRcFile reads the resource file which defines
#  the variables and attributes to be written to the netCDF file.
#\\
#\\
# !INTERFACE:
#
sub readRcFile($) {
#
# !INPUT PARAMETERS:
#
  # $fileName : File containing default settings to be read
  my ( $fileName ) = @_;
#
# !CALLING SEQUENCE:
# &readRcFile( RESOURCE-FILE-NAME );
#
# !REVISION HISTORY:
#  27 Jan 2012 - R. Yantosca - Initial version
#  27 Jan 2012 - R. Yantosca - Now get output filename from the resource file
#EOP
#------------------------------------------------------------------------------
#BOC
#
# !LOCAL VARIABLES:
#
  my $cmdFile = "";
  my $line    = "";
  my @lines   = ();
  my $name    = "";

  #----------------------------------------------
  # Read variable settings from the file
  #----------------------------------------------
  open( I, "<$fileName" ) or die "Cannot open $fileName!\n";
  chomp( @lines = <I> );
  close( I );

  #----------------------------------------------
  # Write Fortran commands to the output file
  #----------------------------------------------
  
  # Get the name of the output file
  foreach $line ( @lines ) {
    if ( $line =~ 'Fortran Def File' ) {
      ( $name, $cmdFile ) = &splitLine( $line, '=' );
    }
  }
      
  # Open the file that will ho
  open( O, ">$cmdFile" ) or die "Cannot open $cmdFile\n";

  # Pass thru @lines array so that we can declare Fortran variables
  &writeFortranVars( \*O, @lines );

  # Pass thru @lines array again to write 
  &writeFortranCalls( \*O, @lines );

  #----------------------------------------------
  # Cleanup and quit
  #----------------------------------------------

  # Close output file
  close( O );

  # Return
  return( 0 );
}
#EOP
#------------------------------------------------------------------------------
#          Harvard University Atmospheric Chemistry Modeling Group            !
#------------------------------------------------------------------------------
#BOP
#
# !IROUTINE: writeFortranVars
#
# !DESCRIPTION:  Routine writeFortranVars writes all of the necessary
#  Fortran variables for using the NcdfUtilities routines.
#\\
#\\
# !INTERFACE:
#
sub writeFortranVars($@) {
#
# !INPUT PARAMETERS:
#
  # $O     : File handle 
  # @lines : Contents of the resource file
  my ( $O, @lines ) = @_;
#
# !CALLING SEQUENCE:
# @lines = &readRcFile( FILENAME );
#
# !REVISION HISTORY:
#  24 Oct 2011 - R. Yantosca - Initial version, based on GEOS_5/perl/doGeos5
#EOP
#------------------------------------------------------------------------------
#BOC
#
# !LOCAL VARIABLES:
#
  my @subStr = ();
  my $name   = "";
  my $value  = "";
  my $txt    = "";

  #-------------------------------------------------------
  # Write USE statements 
  #-------------------------------------------------------
  $txt  = <<EOF;
      ! Modules for netCDF write
      USE m_netcdf_io_create
      USE m_netcdf_io_define
      USE m_netcdf_io_write
      USE m_netcdf_io_close

      IMPLICIT NONE
      PRIVATE

\#     include "netcdf.inc"

      !=================================================================
      ! Variable declarations
      !=================================================================
EOF
  print O "$txt\n";

  # Loop thru the LINES array
  for ( my $i = 0; $i < scalar( @lines ); $i++ ) {

    # Skip separator line
    if ( $lines[$i] eq '#' ) {
      # Do nothing
    }

    #----------------------------------------------------
    # FILENAME section
    #----------------------------------------------------
    elsif ( $lines[$i] =~ '!FILENAME:' ) {
      		
      while ( $lines[++$i] ne '' ) {
		
	# Split the line on the equals sign
	( $name, $value ) = &splitLine( $lines[$i], '=' );
      
	# Declare netCDF file and variable ID's
	if ( $name =~ 'FileHandle' ) { 
	  $F_ID = $value;
	  $txt  = "      ! Declare netCDF file ID and variable ID\n";
	  $txt .= "      INTEGER            :: $F_ID\n";
	  $txt .= "      INTEGER            :: vId\n";
	  $txt .= "      INTEGER            :: omode\n";
	}	      
      }			       

    #----------------------------------------------------
    # DIMENSIONS section
    #----------------------------------------------------
    } elsif ( $lines[$i] =~ '!DIMENSIONS:' ) {

      # Comment line
      $txt .= "\n      ! Variables for netCDF dimensions\n";

      while ( $lines[++$i] ne '' ) {

	# Split the line 
	( $name, $value ) = &splitLine( $lines[$i], '=' );

	# Write a Fortran var for each netCDF dimension
	$txt .= "      INTEGER            :: id_$name\n";
      }
    }
  }

  #-------------------------------------------------------
  # OTHER VARIABLES section
  #-------------------------------------------------------
  $txt .= "\n      ! Variables for netCDF attributes\n";
  $txt .= "      CHARACTER(LEN=255) :: a_name\n";
  $txt .= "      CHARACTER(LEN=255) :: a_val\n";
  $txt .= "\n      ! Arrays for var ID's, starting points, and var sizes\n";
  $txt .= "      INTEGER            :: var1d(1), st1d(1), ct1d(1)\n";
  $txt .= "      INTEGER            :: var2d(2), st2d(2), ct2d(2)\n";
  $txt .= "      INTEGER            :: var3d(3), st3d(3), ct3d(3)\n";
  $txt .= "      INTEGER            :: var4d(4), st4d(4), ct4d(4)\n";
  $txt .= "      INTEGER            :: var5d(5), st4d(5), ct4d(5)\n";
  print $O "$txt\n";

  # Return
  return( 0 );
}
#EOP
#------------------------------------------------------------------------------
#          Harvard University Atmospheric Chemistry Modeling Group            !
#------------------------------------------------------------------------------
#BOP
#
# !IROUTINE: writeFortranCalls
#
# !DESCRIPTION:  Routine getDefaults reads the default values for 
#  input quantities.
#\\
#\\
# !INTERFACE:
#
sub writeFortranCalls($@) {
#
# !INPUT PARAMETERS:
#
  # $O     : File handle 
  # @lines : Contents of the resource file
  my ( $O, @lines ) = @_;
#
# !REVISION HISTORY:
#  27 Jan 2012 - R. Yantosca - Initial version
#EOP
#------------------------------------------------------------------------------
#BOC
#
# !LOCAL VARIABLES:
#
  my $txt = "";

  #-------------------------------------------------------
  # Add a spacer comment to the Fortran code
  #-------------------------------------------------------
  $txt = <<EOF;
      !=================================================================
      ! %%%%% NETCDF DEFINITION SECTION %%%%%
      !=================================================================

      ! Initialize the variable ID counter
      vId = 0
EOF
  print $O "$txt\n";

  # Loop thru each line in the file
  for ( my $i = 0; $i < scalar( @lines ); $i++ ) {
    				
    # Skip separator line
    if ( $lines[$i] eq '#' ) {
      # Do nothing
    }

    #----------------------------------------------------
    # FILENAME section
    #----------------------------------------------------
    elsif ( $lines[$i] =~ '!FILENAME:' ) {

      while ( $lines[++$i] ne '' ) { 
	if ( !( $lines[$i] =~ '#' ) ) { 
	  &handleFileName( \*O, $lines[$i] );
	}
      }
    }
    
    #----------------------------------------------------
    # GLOBAL ATTRIBUTES section
    #----------------------------------------------------
    elsif ( $lines[$i] =~ '!GLOBAL ATTRIBUTES:' ) {

      # Add a spacer comment to the Fortran code
      $txt  = "      !--------------------------------\n";
      $txt .= "      ! GLOBAL ATTRIBUTES\n";
      $txt .= "      !--------------------------------\n";
      print $O "$txt\n";

      # Write Fortran calls to define global attributes
      while ( $lines[++$i] ne '' ) {
	if ( !( $lines[$i] =~ '#' ) ) {	
	  &handleGlobalAtts( \*O, $lines[$i] );
	}
      }
    }

    #----------------------------------------------------
    # DIMENSIONS section
    #----------------------------------------------------
    elsif ( $lines[$i] =~ '!DIMENSIONS:' ) { 

      # Add a spacer comment to the Fortran code
      $txt  = "      !--------------------------------\n";
      $txt .= "      ! DIMENSIONS\n";
      $txt .= "      !--------------------------------\n";
      print $O "$txt\n";
				
      # Write Fortran calls to define dimensions
      while ( $lines[++$i] ne '' ) {
	if ( !( $lines[$i] =~ '#' ) ) { 
	  &handleDimensions( \*O, $lines[$i] );
	}
      }
    }

    #----------------------------------------------------
    # VARIABLES section
    #----------------------------------------------------
    elsif ( $lines[$i] =~ '!VARIABLES:' ) {

      # Write fortran calls to define variables
      while ( $lines[++$i] ne '' ) {
	if ( !( $lines[$i] =~ '#' ) ) { 
	  &handleVariables( \*O, $lines[$i] ); 
	}
      }
    }
  }

  #-------------------------------------------------------
  # Add a spacer comment to the Fortran code
  #-------------------------------------------------------
  $txt = <<EOF2;
      !=================================================================
      ! %%%%% END OF NETCDF DEFINITION SECTION %%%%%
      !=================================================================
      CALL NcEnd_Def( $F_ID );
EOF2
  print $O "$txt\n";

  # Return 
  return( 0 );
}
#EOP
#------------------------------------------------------------------------------
#          Harvard University Atmospheric Chemistry Modeling Group            !
#------------------------------------------------------------------------------
#BOP
#
# !IROUTINE: handleFileName
#
# !DESCRIPTION:  Routine handleFileName writes the proper Fortran calls
#  to NcdfUtilities routines for opening a netCDF file.
#\\
#\\
# !INTERFACE:
#
sub handleFileName($$) {
#
# !INPUT PARAMETERS:
#
  # $O     : File handle 
  # @lines : A single line from the resource file
  my ( $O, $line ) = @_;
#
# !CALLING SEQUENCE:
# &handleFileName( \*O, $line );
#
# !REVISION HISTORY:
#  27 Jan 2012 - R. Yantosca - Initial version
#EOP
#------------------------------------------------------------------------------
#BOC
#
# !LOCAL VARIABLES:
#
  # Split the name on the equals sign
  my( $name, $value ) = &splitLine( $line, '=' );

  if ( $name =~ 'FileHandle' ) { 
    return(0);
  
  } elsif ( $name =~ 'FileName' ) {
    my $txt = <<EOF;
      ! Open filename
      CALL NcCr_Wr( $F_ID, "$value" )

      ! Turn filling off
      CALL NcSetFill( $F_ID, NF_NOFILL, omode )     
EOF

     # Write to file
     print $O "$txt\n";
  }

  # Return
  return( 0 );
}
#EOP
#------------------------------------------------------------------------------
#          Harvard University Atmospheric Chemistry Modeling Group            !
#------------------------------------------------------------------------------
#BOP
#
# !IROUTINE: handleGlobalAtts
#
# !DESCRIPTION:  Routine handleGlobalAtts writes the proper Fortran calls
#  to NcdfUtilities routines for defining global attributes.
#\\
#\\
# !INTERFACE:
#
sub handleGlobalAtts($$) {
#
# !INPUT PARAMETERS:
#
  # $O     : File handle 
  # @lines : A single line from the resource file
  my ( $O, $line ) = @_;
#
# !CALLING SEQUENCE:
# &handleGlobalAtts( \*O, $line );
#
# !REVISION HISTORY:
#  27 Jan 2012 - R. Yantosca - Initial version
#EOP
#------------------------------------------------------------------------------
#BOC
#
# !LOCAL VARIABLES:
#
  # Split the name on the equals sign
  my( $name, $value ) = &splitLine( $line, '=' );

  # Define the text string
  my $txt = <<EOF;
      ! Define the $name global attribute
      a_name = "$name"
      a_val  = "$value"
      CALL NcDef_Glob_Attributes( $F_ID, TRIM(a_name), TRIM(a_val) )
EOF

  # Write to file
  print $O "$txt\n";

  # Return
  return( 0 );
}
#EOP
#------------------------------------------------------------------------------
#          Harvard University Atmospheric Chemistry Modeling Group            !
#------------------------------------------------------------------------------
#BOP
#
# !IROUTINE: handleDimensions
#
# !DESCRIPTION:  Routine handleDimensions writes the proper Fortran calls
#  to NcdfUtilities routines for defining netCDF dimensions.
#\\
#\\
# !INTERFACE:
#
sub handleDimensions($$) {
#
# !INPUT PARAMETERS:
#
  # $O     : File handle 
  # @lines : A single line from the resource file
  my ( $O, $line ) = @_;
#
# !CALLING SEQUENCE:
# &handleDimensions( \*O, $line );
#
# !REVISION HISTORY:
#  27 Jan 2012 - R. Yantosca - Initial version
#EOP
#------------------------------------------------------------------------------
#BOC
#
# !LOCAL VARIABLES:
#
  # Split the line on the equals sign
  my( $name, $value ) = &splitLine( $line, '=' );

  # HERE doc to define Fortran commands
  my $txt = <<EOF;
      ! Define $name dimension
      CALL NcDef_Dimension( $F_ID, '$name', $value, id_$name )
EOF

  # Write to output file     
  print O "$txt\n";

  # Return
  return( 0 );
}
#EOP
#------------------------------------------------------------------------------
#          Harvard University Atmospheric Chemistry Modeling Group            !
#------------------------------------------------------------------------------
#BOP
#
# !IROUTINE: handleVarialbes
#
# !DESCRIPTION:  Routine handleVariables writes the proper Fortran calls
#  to NcdfUtilities routines for defining variables and variable attributes.
#\\
#\\
# !INTERFACE:
#
sub handleVariables($$) {
#
# !INPUT PARAMETERS:
#
  # $fileName : File containing default settings to be read
  my ( $O, $line ) = @_;
#
# !CALLING SEQUENCE:
# @lines = &readRcFile( FILENAME );
#
# !REVISION HISTORY:
#  27 Jan 2012 - R. Yantosca - Initial version
#EOP
#------------------------------------------------------------------------------
#BOC
#
# !LOCAL VARIABLES:
#
  my $varName  = "";
  my $varSize  = "";
  my $varType  = "";
  my $varDim   = "";
  my $attName  = "";
  my $attValue = "";
  my $NF_TYPE  = "";
  my $nDims    = "";
  my @dims     = ();
  my $dimArr   = "";
  my $dimDef   = "";
  my $txt      = "";

  # Split the line on the equals sign
  my( $name, $value ) = &splitLine( $line, '=' );

  #-----------------------------------------------------------------------
  # If the $name field has a semicolon, then it is an attribute
  #-----------------------------------------------------------------------
  if ( $name =~ ':' ) { 

    # Get The variable name and attribute name
    ( $varName, $attName ) = &splitLine( $name, ':' );

    # Attribute value
    $attValue = $value;
 
    # Write commands to disk
    $txt = <<EOF;
      ! Define the $varName:$attName attribute
      a_name = "$attName"
      a_val  = "$attValue"
      CALL NcDef_Var_Attributes( $F_ID, vId, TRIM(a_name), TRIM(a_val) )
EOF
    print $O "$txt\n";
  }

  #-----------------------------------------------------------------------
  # If the $name field lacks a semicolon, then it is the variable name
  #-----------------------------------------------------------------------
  else {

    # Get the variable name
    $varName = $name; 
   
    # Find the variable type and variable dimension(s)
    ( $varType, $varDim ) = &splitLine( $value, '::' );
    $varType =~ s/\*//g;

    # Define the NF_TYPE for the output
    if    ( $varType =~ 'REAL8'     ) { $NF_TYPE = "NF_DOUBLE"; }
    if    ( $varType =~ 'DOUBLE'    ) { $NF_TYPE = "NF_DOUBLE"; }
    elsif ( $varType =~ 'REAL4'     ) { $NF_TYPE = "NF_FLOAT";  }
    elsif ( $varType =~ 'FLOAT'     ) { $NF_TYPE = "NF_FLOAT";  }
    elsif ( $varType =~ 'INTEGER'   ) { $NF_TYPE = "NF_INT";    }
    elsif ( $varType =~ 'CHARACTER' ) { $NF_TYPE = "NF_CHAR";   }

    # Get dimension information
    @dims = split( ',', $varDim );
    $nDims = scalar( @dims );

    # Dimension array used in call to NcDef_Variable
    $dimArr = "var$nDims"."d";
    $dimDef = "$dimArr = (/ ";
    for ( my $i=0; $i<$nDims; $i++ ) { 
      $dimDef .= "id_$dims[$i]";
      if ( $i < $nDims-1 ) { $dimDef .= ", "; }
    }	
    $dimDef .= " /)";

    # Write commands to disk
    $txt = <<EOF2;
      !--------------------------------
      ! VARIABLE: $varName
      !--------------------------------

      ! Define the $varName variable
      vId   = vId + 1
      $dimDef
      CALL NcDef_Variable( $F_ID, '$varName', $NF_TYPE, $nDims, $dimArr, vId )
EOF2
    print $O "$txt\n";

  }

  # Return
  return( 0 );
}
#EOC
#------------------------------------------------------------------------------
#          Harvard University Atmospheric Chemistry Modeling Group            !
#------------------------------------------------------------------------------
#BOP
#
# !IROUTINE: main
#
# !DESCRIPTION: Routine main is the driver routine for the ncCodeDef script.
#\\
#\\
# !INTERFACE:
#
sub main() {
#
# !CALLING SEQUENCE:
#  &main();
#
# !REVISION HISTORY:
#  24 Oct 2011 - R. Yantosca - Initial version, based on GEOS_5/perl/doGeos5
#EOP
#------------------------------------------------------------------------------
#BOC

  # Error check arguments
  if ( scalar( @ARGV ) == 0 ) { 
    print "Usage: ncCodeDef RESOURCE-FILE\n"; 
    exit(1);
  }

  # Get default values
  &readRcFile( $ARGV[0] );

  # Return normally
  return( 0 );
}
#EOC

#------------------------------------------------------------------------------

# Start main program
main();

# Exit normally
exit(0);
